app_name := "go-project"
entrypoint := "cmd/" + app_name
binary := "tmp/" + app_name
debug := "false"

[private]
@default:
    just --list

@setup:
	just cleanup build

@cleanup:
	rm -rf dist/ tmp/
	find . -type d -empty -delete

@download:
	go mod tidy

@update:
	go get -u ./...

@build:
	go build -tags='dev' -o {{binary}} ./{{entrypoint}}

@run:
	{{binary}}

debug:
	#!/usr/bin/env bash
	while true; do
		dlv debug ./{{entrypoint}} \
		--build-flags="-tags=dev" \
		--headless --listen :2345 --api-version 2
	done

@dev:
	wgo -file .go go mod tidy :: just build :: just run

@dev-debug:
	wgo -file .go go mod tidy :: just debug

@create-release-snapshot:
	goreleaser release --snapshot --clean

create-git-release-tag:
	#!/usr/bin/env bash
	TAG=v$(cat .version)
	echo "Creating git tag $TAG"
	git tag -f -a $TAG -m "Release $TAG"

push-git-release-tag:
	#!/usr/bin/env bash
	TAG=v$(cat .version)
	git push origin $TAG
