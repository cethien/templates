app_name := "go-project"
entrypoint := "cmd/" + app_name
binary := "tmp/" + app_name

[private]
@default:
    just --list

@setup: cleanup build

@cleanup:
	rm -rf dist/ tmp/
	find . -type d -empty -delete

@update:
	go get -u ./...

@build:
	go build -tags='dev' -o {{binary}} ./{{entrypoint}}

@run *args:
	{{binary}} {{args}}

debug:
	#!/usr/bin/env bash
	while true; do
		dlv debug ./{{entrypoint}} \
		--build-flags="-tags=dev" \
		--headless --listen :2345 --api-version 2
	done

@dev *args:
	wgo -file .go go mod tidy :: just build :: just run {{args}}

@dev-debug:
	wgo -file .go go mod tidy :: just debug

@create-release-snapshot:
	goreleaser release --snapshot --clean

create-git-release-tag:
	#!/usr/bin/env bash
	TAG=v$(cat .version)
	echo "Creating git tag $TAG"
	git tag -f -a $TAG -m "Release $TAG"

push-git-release-tag:
	#!/usr/bin/env bash
	TAG=v$(cat .version)
	git push origin $TAG
